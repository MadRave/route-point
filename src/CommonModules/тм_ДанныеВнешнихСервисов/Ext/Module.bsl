// MIT License
//
// Copyright (c) 2025 Maksim Kochemasov
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Область СлужебныйПрограммныйИнтерфейс

// Проверяет наличие данных о внешнем сервисе с указанным идентификатором в базе данных
//
// Параметры:
//  ИдентификаторСервиса - Строка
// 
// Возвращаемое значение:
//   - Булево
//
Функция СервисСИдентификаторомСуществует(ИдентификаторСервиса) Экспорт
	
	Существует = Ложь;
	
	Если НЕ тм_ДанныеВнешнихСервисовПовтИсп.ПолучитьСервисПоИдентификатору(ИдентификаторСервиса).Пустая() Тогда
		Существует = Истина;
	КонецЕсли;
	
	Возврат Существует;
	
КонецФункции

// Проверяет наличие прикладной операции в сервисе с переданным идентификатором
//
// Параметры:
//  ИдентификаторСервиса - Строка - идентификатор сервиса, в котором нужно проверить наличие прикладной операции
//  ИдентификаторОперации - Строка - идентификатор прикладной операции, наличие которой нужно проверить в сервисе
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПрикладнаяОперацияЕстьВСервисе(ИдентификаторСервиса, ИдентификаторОперации) Экспорт
	
	ВнешнийСервис = тм_ДанныеВнешнихСервисовПовтИсп.ПолучитьСервисПоИдентификатору(ИдентификаторСервиса);
	
	Возврат Справочники.тм_ВнешниеСервисы.ПрикладнаяОперацияЕстьВСервисе(ВнешнийСервис, ИдентификаторОперации);
	
КонецФункции

// Возвращает данные для инициализации модуля-обработки прикладной операции
//
// Параметры:
//  ИдентификаторСервиса - Строка - идентификатор внешнего сервиса, содержащего прикладную операцию
//  ИдентификаторОперации - Строка - идентификатор прикладной операции сервиса,
//                          модуль-обработку которой нужно инициализировать
// 
// Возвращаемое значение:
//   - Структура:
//       * МодульAPI - ОпределяемыйТип.тм_МодульАпи - основной модуль API внешнего сервиса
//       * МодульОперации - ОпределяемыйТип.тм_МодульАпи - модуль обработки прикладной операции
//       * ДанныеАутентификации - Строка - адрес во временном хранилище с данными аутентификации
//
Функция ДанныеИнициализацииМодуляПрикладнойОперации(ИдентификаторСервиса, ИдентификаторОперации) Экспорт
	
	ВнешнийСервис = тм_ДанныеВнешнихСервисовПовтИсп.ПолучитьСервисПоИдентификатору(ИдентификаторСервиса);
	
	Если НЕ ЗначениеЗаполнено(ВнешнийСервис) Тогда
		ВызватьИсключение НСтр("ru = 'Не найден внешний сервис с идентификатором '") + ИдентификаторСервиса;
	КонецЕсли;
	
	МодулиОперации = Справочники.тм_ВнешниеСервисы.МодулиПрикладнойОперации(ВнешнийСервис, ИдентификаторОперации);
	ЗначенияДанныхАвторизации = ДанныеАутентификацииСервиса(ВнешнийСервис);
	
	ДанныеИнициализацииМодуляОперации = НовыеДанныеИнициализацииМодулейСервиса();
	ДанныеИнициализацииМодуляОперации.МодульAPI = МодулиОперации.МодульAPI;
	ДанныеИнициализацииМодуляОперации.МодульОперации = МодулиОперации.МодульОперации;
	ДанныеИнициализацииМодуляОперации.ДанныеАутентификации = ПоместитьВоВременноеХранилище(ЗначенияДанныхАвторизации);
	
	Возврат ДанныеИнициализацииМодуляОперации;
	
КонецФункции

// Возвращает данные для инициализации основного модуля-обработки API для
// взаимодействия с внешним сервисом.
// Для инициализации Модуля API без модуля прикладной операции. Если также требуется
// инициализировать модуль основной операции следует вызывать
// тм_ДанныеВнешнихСервисов.ДанныеИнициализацииМодуляПрикладнойОперации()
//
// Параметры:
//  ИдентификаторСервиса - Строка - идентификатор внешнего сервиса, модуль API которого нужно инициализировать
// 
// Возвращаемое значение:
//   - Структура:
//       * МодульAPI - ОпределяемыйТип.тм_МодульАпи - ссылка на основную модуль-обрботку внешнего сервиса
//       * ДанныеАутентификации - Строка - адрес во временном хранилище с данными аутентификации
//
Функция ДанныеИнициализацииОсновногоМодуляСервиса(ИдентификаторСервиса) Экспорт
	
	ВнешнийСервис = тм_ДанныеВнешнихСервисовПовтИсп.ПолучитьСервисПоИдентификатору(ИдентификаторСервиса);
	
	Если НЕ ЗначениеЗаполнено(ВнешнийСервис) Тогда
		ВызватьИсключение НСтр("ru = 'Не найден внешний сервис с идентификатором '") + ИдентификаторСервиса;
	КонецЕсли;
	
	МодульAPI = Справочники.тм_ВнешниеСервисы.ОсновнойМодульСервиса(ВнешнийСервис);
	ЗначенияДанныхАвторизации = ДанныеАутентификацииСервиса(ВнешнийСервис);
	
	ДанныеИнициализацииОсновногоМодуля = НовыеДанныеИнициализацииМодулейСервиса();
	ДанныеИнициализацииОсновногоМодуля.МодульAPI = МодульAPI;
	ДанныеИнициализацииОсновногоМодуля.ДанныеАутентификации = ПоместитьВоВременноеХранилище(ЗначенияДанныхАвторизации);
	
	Возврат ДанныеИнициализацииОсновногоМодуля;
	
КонецФункции

// Сохраняет данные аутентификации для подключения к внешнему сервису
//
// Параметры:
//  ИдентификаторАутентификации - Строка - уникальный идентификатор
//  ДанныеАутентификации - Соответствие - соответствие с данными аутентификации для подключения
//
Процедура ЗаписатьДанныеАутентификацииСервиса(ИдентификаторАутентификации, ДанныеАутентификации) Экспорт
	
	Если НЕ тм_ТочкаМаршрутизацииСлужебныйФункционал.ЭтоУникальныйИдентификатор(ИдентификаторАутентификации) Тогда
		ВызватьИсключение НСтр("ru = 'Передан некорректный идентификатор аутентификации "
			+ "для записи учетных данных подключения к сервису.'");
	КонецЕсли;
	
	Если ТипЗнч(ДанныеАутентификации) <> Тип("Соответствие") Тогда
		ВызватьИсключение НСтр("ru = 'Передан некорректный формат данных аутентификации'");
	КонецЕсли;
	
	ЗаписатьДанныеАутентификацииВХранилище(ИдентификаторАутентификации, ДанныеАутентификации);
	
КонецПроцедуры

// Удаляет данные аутентификации подключения к внешнему сервису
// с переданным идентификатором аутентификации
//
// Параметры:
//  ИдентификаторАутентификации - Строка - идентификатор аутентификации, для которого нужно удалить
//                                данные аутентификации из хранилища
//
Процедура УдалитьДанныеАутентификацииСервиса(ИдентификаторАутентификации) Экспорт
	
	УдалитьДанныеАутентификацииИзХранилища(ИдентификаторАутентификации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДанныеАутентификации

// Параметры:
//  ИдентификаторАутентификации - Строка - уникальный идентификатор
//  ДанныеАутентификации - Соответствие
//
Процедура ЗаписатьДанныеАутентификацииВХранилище(ИдентификаторАутентификации, ДанныеАутентификации)
	
	тм_ДанныеВнешнихСервисовПереопределяемый.ПриЗаписиДанныхАутентификацийВХранилище(
		ИдентификаторАутентификации, ДанныеАутентификации);
	
КонецПроцедуры

// Параметры:
//  ИдентификаторАутентификации - Строка - уникальный идентификатор данных аутентификации, которые нужно удалить
//
Процедура УдалитьДанныеАутентификацииИзХранилища(ИдентификаторАутентификации)
	
	тм_ДанныеВнешнихСервисовПереопределяемый.ПриУдаленииДанныхАутентификацииИзХранилища(ИдентификаторАутентификации);
	
КонецПроцедуры

// Параметры:
//  АдресВх - Строка - адрес временного хранилица
//  ИдентификаторАутентификации - Строка - уникальный идентификатор владельца данных авторизации
//
Процедура ПоместитьДанныеАутентификации(АдресВх, ИдентификаторАутентификации) Экспорт
	
	ДанныеАутентификаци = ДанныеАутентификацииИзХранилища(ИдентификаторАутентификации);
	
	ПоместитьВоВременноеХранилище(ДанныеАутентификаци, АдресВх);
	
КонецПроцедуры

// Возвращает данные аутентификации для подключения к переданному сервису
//
// Параметры:
//  Сервис - СправочникСсылка.тм_ВнешниеСервисы
// 
// Возвращаемое значение:
//   - Соответствие из КлючЗначение:
//       * Ключ - Строка - тип данных аутентификации (Логин, Пароль, Токен и т.д.)
//       * Значение - Строка - значение данных аутентификации
//
Функция ДанныеАутентификацииСервиса(Сервис)
	
	ИдентификаторАутентификации = Справочники.тм_ВнешниеСервисы.ИдентификаторАутентификацииСервиса(Сервис);
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторАутентификации) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен идентификатор аутентификации для сервиса" + Строка(Сервис) + "'");
	КонецЕсли;
	
	ДанныеАутентификации = ДанныеАутентификацииИзХранилища(ИдентификаторАутентификации);
	
	Возврат ДанныеАутентификации
	
КонецФункции

// Параметры:
//  ИдентификаторАутентификации - Строка - уникальный идентификатор данных аутентификации,
//                                         которые нужно получить из хранилища
// 
// Возвращаемое значение:
//   - Соответствие
//
Функция ДанныеАутентификацииИзХранилища(ИдентификаторАутентификации)
	
	ДанныеАутентификации = Новый Соответствие;
	
	тм_ДанныеВнешнихСервисовПереопределяемый.ПриПолученииДанныхАутентификацииИзХранилища(
		ИдентификаторАутентификации, ДанныеАутентификации);
	
	Если Не ТипЗнч(ДанныеАутентификации) = Тип("Соответствие") Тогда
		ДанныеАутентификации = Новый Соответствие;
	КонецЕсли;
	
	Возврат ДанныеАутентификации;
	
КонецФункции

#КонецОбласти

// Возвращаемое значение:
//   - Структура:
//       * МодульAPI - ОпределяемыйТип.тм_МодульАпи - основной модуль API внешнего сервиса
//       * МодульОперации - ОпределяемыйТип.тм_МодульАпи - модуль обработки прикладной операции
//       * ДанныеАутентификации - Строка - адрес во временном хранилище с данными аутентификации
//
Функция НовыеДанныеИнициализацииМодулейСервиса()
	
	ДанныеИнициализацииМодулей = Новый Структура;
	ДанныеИнициализацииМодулей.Вставить("МодульAPI");
	ДанныеИнициализацииМодулей.Вставить("МодульОперации");
	ДанныеИнициализацииМодулей.Вставить("ДанныеАутентификации", "");
	
	Возврат ДанныеИнициализацииМодулей;
	
КонецФункции

#КонецОбласти

