// MIT License
//
// Copyright (c) 2025 Maksim Kochemasov
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Область ПрограммныйИнтерфейс

// Точка входа для запроса при обращении к прикладной операции
//
// Параметры:
//  ЗапросHTTP - HTTPСервисЗапрос - входящий запрос, который нужно обработать в рамках прикладной операции
//  ИдентификаторСервиса - Строка - идентификатор сервиса, которому адресован запрос
//  ИдентификаторОперации - Строка - идентификатор прикладной операции, относящейся ко внешнему сервису
//  ВызываемыйМетодHTTP - ПеречислениеСсылка.тм_ДоступныеМетодыОбработкиЗапросов - метод входящего запроса HTTP
// 
// Возвращаемое значение:
//   - HTTPСервисОтвет - результат выполнения прикладной операции
//
Функция ПриПолученииЗапросаДляПрикладнойОперации(ЗапросHTTP, ИдентификаторСервиса, ИдентификаторОперации, 
	ВызываемыйМетодHTTP) Экспорт
	
	РезультатПроверкиНаличияСервисаИОперации = ПроверитьНаличиеСервисаИОперации(ИдентификаторСервиса, ИдентификаторОперации);
	
	Если НЕ РезультатПроверкиНаличияСервисаИОперации.Успешно Тогда
		КодСостояния = СостоянияОтвета().НеНайден;
		Возврат ОтветОшибка(КодСостояния, РезультатПроверкиНаличияСервисаИОперации.ТекстОшибки);
	КонецЕсли;
	
	ДанныеДляИнициализацииМодуля = тм_ДанныеВнешнихСервисов
		.ДанныеИнициализацииМодуляПрикладнойОперации(ИдентификаторСервиса, ИдентификаторОперации);
	
	РезультатПроверкиМодулей = ПроверитьНаличиеМодулейПрикладнойОперации(ДанныеДляИнициализацииМодуля,
		ИдентификаторСервиса, ИдентификаторОперации);
	
	Если НЕ РезультатПроверкиМодулей.Успешно Тогда
		КодСостояния = СостоянияОтвета().ОтсутствуетМодуль;
		Возврат ОтветОшибка(КодСостояния, РезультатПроверкиМодулей.ТекстОшибки);
	КонецЕсли;
	
	КонтекстОбработкиЗапроса = тм_УправлениеМаршрутизациейСлужебный.ПолучитьКонтекстПоМетодуВходящегоЗапроса(
		ВызываемыйМетодHTTP);
	
	Если КонтекстОбработкиЗапроса = Неопределено Тогда
		КодСостояния = СостоянияОтвета().ОтсутствуетФункционал;
		Возврат ОтветОшибка(КодСостояния, НСтр("ru = 'Вызываемый метод не поддерживается для текущего ресурса'"));
	КонецЕсли;
	
	МодульОперации = тм_УправлениеМаршрутизациейСлужебный.МодульПрикладнойОперации(ДанныеДляИнициализацииМодуля);
	
	Возврат КонтекстОбработкиЗапроса.ВыполнитьКомандуПрикладнойОперации(МодульОперации, ЗапросHTTP);
	
КонецФункции

// Точка входа для запроса при обращении к основному модулю внешнего сервиса
//
// Параметры:
//  ЗапросHTTP - HTTPСервисЗапрос - входящий запрос, который должен обработать основной модуль API внешнего сервиса
//  ИдентификаторСервиса - Строка - идентификатор сервиса, которому адресован запрос
//  ИдентификаторКоманды - Строка - идентификатор команды модуля API внешнего сервиса, которую нужно выполнить
//  ВызываемыйМетодHTTP - ПеречислениеСсылка.тм_ДоступныеМетодыОбработкиЗапросов - метод входящего запроса HTTP
// 
// Возвращаемое значение:
//   - HTTPСервисОтвет - результат выполнения команды модуля API внешнего сервиса
//
Функция ПриПолученииЗапросаДляОсновногоМодуляСервиса(ЗапросHTTP, ИдентификаторСервиса, ИдентификаторКоманды,
	ВызываемыйМетодHTTP) Экспорт
	
	Если НЕ тм_ДанныеВнешнихСервисов.СервисСИдентификаторомСуществует(ИдентификаторСервиса) Тогда
		КодСостояния = СостоянияОтвета().НеНайден;
		ТекстОшибки = НСтр("ru = 'Не найден внешний сервис с идентификатором '") + ИдентификаторСервиса;
		Возврат ОтветОшибка(КодСостояния, ТекстОшибки);
	КонецЕсли;
	
	ДанныеДляИнициализацииМодуляAPI = тм_ДанныеВнешнихСервисов.ДанныеИнициализацииОсновногоМодуляСервиса(ИдентификаторСервиса);
	
	Если НЕ ЗначениеЗаполнено(ДанныеДляИнициализацииМодуляAPI.МодульAPI) Тогда
		КодСостояния = СостоянияОтвета().ОтсутствуетМодуль;
		ТекстОшибки = НСтр("ru = 'Не найден модуль API сервиса '") + ИдентификаторСервиса;
		Возврат ОтветОшибка(КодСостояния, ТекстОшибки);
	КонецЕсли;
	
	КонтекстОбработкиЗапроса = тм_УправлениеМаршрутизациейСлужебный.ПолучитьКонтекстПоМетодуВходящегоЗапроса(
		ВызываемыйМетодHTTP);
	
	Если КонтекстОбработкиЗапроса = Неопределено Тогда
		КодСостояния = СостоянияОтвета().ОтсутствуетФункционал;
		Возврат ОтветОшибка(КодСостояния, НСтр("ru = 'Вызываемый метод не поддерживается для текущего ресурса'"));
	КонецЕсли;
	
	МодульAPI = тм_УправлениеМаршрутизациейСлужебный.ОсновнойМодульAPIВнешнегоСервиса(ДанныеДляИнициализацииМодуляAPI);
	
	Возврат КонтекстОбработкиЗапроса.ВыполнитьКомандуОсновногоМодуляВнешнегоСервиса(МодульAPI, ЗапросHTTP,
		ИдентификаторКоманды);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает ответ с ошибкой
//
// Параметры:
//  КодСостояния - Число
//  ТекстОшибки - Строка
// 
// Возвращаемое значение:
//   - HTTPСервисОтвет
//
Функция ОтветОшибка(КодСостояния, ТекстОшибки)
	
	ЗаголовкиОтвета = Новый Соответствие;
	ЗаголовкиОтвета.Вставить("Content-type", "text/html; charset=utf-8");
	
	Ответ = Новый HTTPСервисОтвет(КодСостояния, , ЗаголовкиОтвета);
	Ответ.УстановитьТелоИзСтроки(ТекстОшибки, КодировкаТекста.UTF8);
	
	Возврат Ответ;
	
КонецФункции

// Возвращаемое значение:
//   - Структура:
//       * Успешно - Число - код состояния 200
//       * НеНайден - Число - код состояния 400
//       * ОтсутствуетМодуль - Число - код состояния 403
//       * ОтсутствуетФункционал - Число - код состояния 501
//
Функция СостоянияОтвета()
	
	СостоянияОтвета = Новый Структура;
	СостоянияОтвета.Вставить("Успешно", 200);
	СостоянияОтвета.Вставить("НеНайден", 400);
	СостоянияОтвета.Вставить("ОтсутствуетМодуль", 403);
	СостоянияОтвета.Вставить("ОтсутствуетФункционал", 501);
	
	Возврат СостоянияОтвета;
	
КонецФункции

// Проверяет наличие информации о сервисе и прикладной операции в базе
//
// Параметры:
//  ИдентификаторСервиса - Строка - идентификатор внешнего сервиса
//  ИдентификаторОперации - Строка - идентификатор прикладной операции внешнего сервиса
// 
// Возвращаемое значение:
//   - Структура - см. НовыйРезультатПроверкиДанных()
//
Функция ПроверитьНаличиеСервисаИОперации(ИдентификаторСервиса, ИдентификаторОперации)
	
	РезультатПроверки = НовыйРезультатПроверкиДанных();
	
	Если НЕ тм_ДанныеВнешнихСервисов.СервисСИдентификаторомСуществует(ИдентификаторСервиса) Тогда
		
		РезультатПроверки.ТекстОшибки = НСтр("ru = 'Не найден внешний сервис с идентификатором '") + ИдентификаторСервиса;
		
	ИначеЕсли НЕ тм_ДанныеВнешнихСервисов.ПрикладнаяОперацияЕстьВСервисе(ИдентификаторСервиса, ИдентификаторОперации) Тогда
		
		РезультатПроверки.ТекстОшибки = СтрШаблон(НСтр("ru = 'Не найдена прикладная операция %1 в сервисе %2'"),
			ИдентификаторОперации, ИдентификаторСервиса);
		
	Иначе
		РезультатПроверки.Успешно = Истина;
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Проверяет наличие модулей для выполнения прикладной операции внешнего сервиса
//
// Параметры:
//  ДанныеДляИнициализацииМодуля - Структура - см. тм_ДанныеВнешнихСервисов.ДанныеИнициализацииМодуляПрикладнойОперации()
//  ИдентификаторСервиса - Строка - идентификатор проверяемого сервиса
//  ИдентификаторОперации - Строка - идентификатор проверяемой прикладной операции внешнего сервиса
// 
// Возвращаемое значение:
//   - Структура - см. НовыйРезультатПроверкиДанных()
//
Функция ПроверитьНаличиеМодулейПрикладнойОперации(ДанныеДляИнициализацииМодуля, ИдентификаторСервиса,
	ИдентификаторОперации)
	
	РезультатПроверки = НовыйРезультатПроверкиДанных();
	
	Если НЕ ЗначениеЗаполнено(ДанныеДляИнициализацииМодуля.МодульAPI) Тогда
		
		РезультатПроверки.ТекстОшибки = НСтр("ru = 'Не найден модуль API сервиса '") + ИдентификаторСервиса;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(ДанныеДляИнициализацииМодуля.МодульОперации) Тогда
		
		РезультатПроверки.ТекстОшибки = СтрШаблон(НСтр("ru = 'Не найден модуль прикладной операции %1 сервиса %2'"),
			ИдентификаторОперации, ИдентификаторСервиса);
		
	Иначе
		РезультатПроверки.Успешно = Истина;
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Возвращаемое значение:
//   - Структура:
//       * Успешно - Булево
//       * ТекстОшибки - Строка
//
Функция НовыйРезультатПроверкиДанных()
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("Успешно", Ложь);
	РезультатПроверки.Вставить("ТекстОшибки", "");
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти