// MIT License
//
// Copyright (c) 2025 Maksim Kochemasov
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Область СлужебныйПрограммныйИнтерфейс

// Проверяет, правильно ли заполнен переданный идентификатор внешнего сервиса
// или прикладной операции. Валидный идентификатор должен содержать только
// латинские символы, числа, символы "_" и "-". Использование других символов
// в идентификаторах внешних сервисов и прикладных операций не допускается
//
// Параметры:
//  ИдентификаторСервисаИлиОперации - Строка - идентификатор внешнего сервиса
//                                             или прикладной операции для проверки
// 
// Возвращаемое значение:
//   - Структура:
//       * ЭтоДопустимыйИдентификатор - Булево - признак валидности переданного идентификатора
//       * ОшибкиЗаполнения - Массив из Строка - текст ошибок заполнения переданного идентификатора
//
Функция РезультатПроверкиИдентификатораСервисаИлиОперации(ИдентификаторСервисаИлиОперации) Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ЭтоДопустимыйИдентификатор", Истина);
	РезультатПроверки.Вставить("ОшибкиЗаполнения", Новый Массив);
	
	// Границы диапазонов символов в таблице ASCII кодов
	ЛатиницаЗаглавнаяДиапазонНачало = 65;
	ЛатиницаЗаглавнаяДиапазонКонец = 90;
	
	ЛатиницаСтрочнаяДиапазонНачало = 97;
	ЛатиницаСтрочнаяДиапазонКонец = 122;
	
	ЧислаДиапазонНачало = 48;
	ЧислаДиапазонКонец = 57;
	
	СимволПодчеркивание = КодСимвола("_");
	СимволПеренос = КодСимвола("-");
	
	СтрокаДляПроверки = ИдентификаторСервисаИлиОперации;
	
	Пока СтрДлина(СтрокаДляПроверки) > 0 И РезультатПроверки.ЭтоДопустимыйИдентификатор Цикл
		
		СимволПроверки = КодСимвола(Лев(СтрокаДляПроверки, 1));
		
		ЭтоДопустимыйСимвол =
			(СимволПроверки >= ЛатиницаЗаглавнаяДиапазонНачало И СимволПроверки <= ЛатиницаЗаглавнаяДиапазонКонец)
			ИЛИ (СимволПроверки >= ЛатиницаСтрочнаяДиапазонНачало И СимволПроверки <= ЛатиницаСтрочнаяДиапазонКонец)
			ИЛИ (СимволПроверки >= ЧислаДиапазонНачало И СимволПроверки <= ЧислаДиапазонКонец)
			ИЛИ (СимволПроверки = СимволПодчеркивание ИЛИ СимволПроверки = СимволПеренос);
		
		Если НЕ ЭтоДопустимыйСимвол Тогда
			
			ТекстОшибки = НСтр("ru = 'Идентификатор сервиса должен содержать только латинские буквы, "
				+ "цифры и символы ""_"", ""-""'");
			
			РезультатПроверки.ЭтоДопустимыйИдентификатор = Ложь;
			РезультатПроверки.ОшибкиЗаполнения.Добавить(ТекстОшибки);
			
			Продолжить;
			
		КонецЕсли;
		
		СтрокаДляПроверки = Сред(СтрокаДляПроверки, 2);
		
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Выводит пользователю служебное сообщение ошибки заполнения реквизита переданного объекта
//
// Параметры:
//  ТекстОшибки - Строка - текст ошибки заполнения реквизита
//  Объект - Произвольный - СправочникОбъект, ДокументОбъект и т.д. Объект, из модуля которого выполняется вызов
//  ИмяРеквизита - Строка - имя реквизита, к которому относится ошибка
//
Процедура СообщитьОшибкуЗаполненияРеквизитаОбъекта(ТекстОшибки, Объект, ИмяРеквизита) Экспорт
	
	СообщениеОшибки = Новый СообщениеПользователю;
	СообщениеОшибки.Текст = ТекстОшибки;
	СообщениеОшибки.Поле = ИмяРеквизита;
	СообщениеОшибки.УстановитьДанные(Объект);
	СообщениеОшибки.Сообщить();
	
КонецПроцедуры

// Выводит пользователю служебное сообщение ошибки заполнения реквизита строки табличной части
// переданного объекта
//
// Параметры:
//  ТекстОшибки - Строка - текст ошибки заполнения реквизита строки табличной части
//  Объект - Произвольный - СправочникОбъект, ДокументОбъект и т.д. Объект из модуля которого выполняется вызов
//  ИмяТаблицы - Строка - имя табличной части объекта
//  ИмяРеквизитаСтроки - Строка - имя реквизита табличной части, в заполнении которого возникли ошибки
//  НомерСтроки - Число - номер строки, в которой возникла ошибка
//
Процедура СообщитьОшибкуЗаполненияРеквизитаСтрокиТаблицыОбъекта(ТекстОшибки, Объект, ИмяТаблицы, 
	ИмяРеквизитаСтроки, НомерСтроки) Экспорт
	
	ПутьКСтрокеТабличнойЧасти = СтрШаблон("%1[%2].%3",
		ИмяТаблицы, Формат(НомерСтроки - 1, "ЧН=0; ЧГ=0"), ИмяРеквизитаСтроки);
	
	СообщитьОшибкуЗаполненияРеквизитаОбъекта(ТекстОшибки, Объект, ПутьКСтрокеТабличнойЧасти);
	
КонецПроцедуры

// Показывает служебное сообщение пользователю
//
// Параметры:
//  ТекстСообщения - Строка
//
Процедура СообщитьПользователю(ТекстСообщения) Экспорт
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();
	
КонецПроцедуры

// Проверяет, является ли переданная строка уникальным идентификатором
//
// Параметры:
//  Строка - Строка - строка для проверки
// 
// Возвращаемое значение:
//   - Булево
//
Функция ЭтоУникальныйИдентификатор(Строка) Экспорт
	
	ЭтоУникальныйИдентификатор = Ложь;
	СтрокаДляПроверки = Строка;
	
	Если СтрДлина(СтрокаДляПроверки) <> 36 Тогда
		Возврат ЭтоУникальныйИдентификатор;
	КонецЕсли;
	
	Разделитель = "-";
	СоставИдентификатора = СтрРазделить(СтрокаДляПроверки, Разделитель, Ложь);
	
	Если СоставИдентификатора.Количество() <> 5 Тогда
		Возврат ЭтоУникальныйИдентификатор;
	КонецЕсли;
	
	ДопустимыеСимволы = "0123456789abcdefABCDEF";
	
	КоличествоСимволовВЧастях = Новый Соответствие;
	КоличествоСимволовВЧастях.Вставить(0, 8);
	КоличествоСимволовВЧастях.Вставить(1, 4);
	КоличествоСимволовВЧастях.Вставить(2, 4);
	КоличествоСимволовВЧастях.Вставить(3, 4);
	КоличествоСимволовВЧастях.Вставить(4, 12);
	
	ИдентификаторВалидный = Истина;
	
	Для сч = 0 По 4 Цикл
		
		ЧастьИдентификатораВалидна = СтрРазделить(СоставИдентификатора[сч], ДопустимыеСимволы, Ложь).Количество() = 0
			И СтрДлина(СоставИдентификатора[сч]) = КоличествоСимволовВЧастях[сч];
		
		ИдентификаторВалидный = ИдентификаторВалидный И ЧастьИдентификатораВалидна;
		
	КонецЦикла;
	
	ЭтоУникальныйИдентификатор = ИдентификаторВалидный;
	
	Возврат ЭтоУникальныйИдентификатор;
	
КонецФункции

#КонецОбласти




























