// MIT License
//
// Copyright (c) 2025 Maksim Kochemasov
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает модуль прикладной операции по взаимодействию с внешним сервисом
//
// Параметры:
//  ПараметрыИнициализацииМодуля - Структура:
//    * МодульAPI - ОпределяемыйТип.тм_МодульАпи - основной модуль API внешнего сервиса
//    * МодульОперации - ОпределяемыйТип.тм_МодульАпи - модуль обработки прикладной операции
//    * ДанныеАутентификации - Строка - адрес во временном хранилище с данными аутентификации
// 
// Возвращаемое значение:
//   - ВнешняяОбработкаОбъект - инициализированный модуль прикладной операции для взаимодействия с внешним сервисом
//
Функция СобратьМодульПрикладнойОперации(ПараметрыИнициализацииМодуля) Экспорт
	
	МодульОперации = ВнешняяОбработкаМодуляАпиИлиПрикладнойОперации(ПараметрыИнициализацииМодуля.МодульОперации);
	МодульОперации.МодульAPI = СобратьОсновнойМодульВзаимодействияСВнешнимСервисом(ПараметрыИнициализацииМодуля);
	
	Возврат МодульОперации;
	
КонецФункции

// Возвращает основной модуль взаимодействия с внешним сервисом
//
// Параметры:
//  ПараметрыИнициализацииМодуля - Структура:
//    * МодульAPI - ОпределяемыйТип.тм_МодульАпи - основной модуль API внешнего сервиса
//    * ДанныеАутентификации - Строка - адрес во временном хранилище с данными аутентификации
// 
// Возвращаемое значение:
//   - ВнешняяОбработкаОбъект - инициализированный модуль API внешнего сервиса
//
Функция СобратьОсновнойМодульВзаимодействияСВнешнимСервисом(ПараметрыИнициализацииМодуля) Экспорт
	
	ОсновнойМодуль = ВнешняяОбработкаМодуляАпиИлиПрикладнойОперации(ПараметрыИнициализацииМодуля.МодульAPI);
	ОсновнойМодуль.ДанныеАутентификации = ПолучитьИзВременногоХранилища(
		ПараметрыИнициализацииМодуля.ДанныеАутентификации);
	
	Возврат ОсновнойМодуль;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает внешнюю обработку модуля АПИ или прикладной операции
// внешнего сервиса
//
// Параметры:
//  ЭлементХраненияОбработки - ОпределяемыйТип.тм_МодульАпи - элемент, в котором хранится внешняя обработка
//                                                            модуля АПИ или прикладной операции внешнего сервиса
// Возвращаемое значение:
//   - ВнешняяОбработкаОбъект - обработка модуля АПИ или прикладной операции внешнего сервиса
//
Функция ВнешняяОбработкаМодуляАпиИлиПрикладнойОперации(ЭлементХраненияОбработки)
	
	Перем ОбработкаМодуля;
	
	тм_ДанныеВнешнихСервисовПереопределяемый.ПриПолученииВнешнейОбработкиМодуляВнешнегоСервиса(ЭлементХраненияОбработки,
		ОбработкаМодуля);
	
	Если ОбработкаМодуля = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Модуль АПИ или прикладной операции не инициализирован'");
	КонецЕсли;
	
	Возврат ОбработкаМодуля;
	
КонецФункции

#КонецОбласти

#КонецЕсли