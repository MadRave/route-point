// MIT License
//
// Copyright (c) 2025 Maksim Kochemasov
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресВхДанныеАвторизации) Тогда
		ДанныеАутентификации = ПолучитьИзВременногоХранилища(АдресВхДанныеАвторизации);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ДанныеАутентификации", ДанныеАутентификации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработчикВводаОписания = Новый ОписаниеОповещения("ПослеВводаОписания", ЭтотОбъект);
	
	ПоказатьВводСтроки(ОбработчикВводаОписания, Объект.Описание, НСтр("ru = 'Описание сервиса'"), , Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПрикладныеОперации

&НаКлиенте
Процедура ПрикладныеОперацииОписаниеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеПрикладнойОперации = Элементы.ПрикладныеОперации.ТекущиеДанные; 
	
	ОбработчикВводаОписанияПрикладнойОперации = Новый ОписаниеОповещения("ПослеВводаОписанияПрикладнойОперации",
		ЭтотОбъект, ДанныеПрикладнойОперации);
	
	ПоказатьВводСтроки(ОбработчикВводаОписанияПрикладнойОперации, ДанныеПрикладнойОперации.Описание,
		НСтр("ru = 'Описание прикладной операции'"), , Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВвестиДанныеАутентификации(Команда)
	
	тм_ДанныеВнешнихСервисовКлиент.ПриВводеДанныхАутентификации(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик ввода описания внешнего сервиса
//
// Параметры:
//  ОписаниеСервиса - Строка
//  ДополнительныеПараметры - Произвольный - не используется
//
&НаКлиенте
Процедура ПослеВводаОписания(ОписаниеСервиса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ОписаниеСервиса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Описание = ОписаниеСервиса;
	
КонецПроцедуры

// Обработчик ввода описания прикладной операции
//
// Параметры:
//  ОписаниеПрикладнойОперации - Строка
//  ДанныеПрикладнойОперации - ДанныеФормыСтруктура
//
&НаКлиенте
Процедура ПослеВводаОписанияПрикладнойОперации(ОписаниеПрикладнойОперации, ДанныеПрикладнойОперации) Экспорт
	
	Если ОписаниеПрикладнойОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПрикладнойОперации.Описание = ОписаниеПрикладнойОперации;
	
КонецПроцедуры

#КонецОбласти

