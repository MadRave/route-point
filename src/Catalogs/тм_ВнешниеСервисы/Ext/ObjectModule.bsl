// MIT License
//
// Copyright (c) 2025 Maksim Kochemasov
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		Если НЕ ЗначениеЗаполнено(ИдентификаторДанныхАвторизации) Тогда
			ИдентификаторДанныхАвторизации = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ИдентификаторСервиса) Тогда
			Отказ = Истина;
			тм_ТочкаМаршрутизацииСлужебныйФункционал.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Идентификатор сервиса""'"));
		КонецЕсли;
		
		Если ЭтоНовый()
			И Справочники.тм_ВнешниеСервисы.СервисСИдентификаторомУжеСуществует(ИдентификаторСервиса) Тогда
			
			Отказ = Истина;
			тм_ТочкаМаршрутизацииСлужебныйФункционал.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Сервис с идентификатором %1 уже записан'"), ИдентификаторСервиса));
			
		КонецЕсли;
		
		Если НЕ ПроверитьУникальностьИдентификаторовОпераций() Тогда
			Отказ = Истина;
			тм_ТочкаМаршрутизацииСлужебныйФункционал.СообщитьПользователю(
				НСтр("ru = 'Идентификаторы прикладных операций должны быть уникальнымии в пределах сервиса'"));
		КонецЕсли;
		
		УдалитьНачальныеИКонечныеПробелыВИдентификаторах();
		
		Если НЕ ПроверитьИдентификаторыСервисаИОпераций() Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ДанныеАутентификации") Тогда
		тм_ДанныеВнешнихСервисов.ЗаписатьДанныеАутентификацииСервиса(ИдентификаторДанныхАвторизации,
			ДополнительныеСвойства.ДанныеАутентификации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторДанныхАвторизации) Тогда
		тм_ДанныеВнешнихСервисов.УдалитьДанныеАутентификацииСервиса(ИдентификаторДанныхАвторизации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет уникальность идентификаторов прикладных операций,
// добавленных в текущем сервисе
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПроверитьУникальностьИдентификаторовОпераций()
	
	ИдентификаторыОперацийУникальны = Истина;
	
	Для Каждого ДанныеОперации Из ПрикладныеОперации Цикл
		
		ДобавленныеОперацииСИдентификатором = ПрикладныеОперации.НайтиСтроки(
			Новый Структура("ИдентификаторОперации", ДанныеОперации.ИдентификаторОперации));
		
		Если ДобавленныеОперацииСИдентификатором.Количество() = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Дублируется идентификатор ""%1"" в строке <%2>'"),
			ДанныеОперации.ИдентификаторОперации, ДанныеОперации.НомерСтроки);
		
		тм_ТочкаМаршрутизацииСлужебныйФункционал.СообщитьОшибкуЗаполненияРеквизитаСтрокиТаблицыОбъекта(
			ТекстСообщения, ЭтотОбъект, "ПрикладныеОперации", "ИдентификаторОперации", ДанныеОперации.НомерСтроки);
		
		ИдентификаторыОперацийУникальны = Ложь;
		
	КонецЦикла;
	
	Возврат ИдентификаторыОперацийУникальны;
	
КонецФункции

// Удаляет пробелы в начале и в конце строки идентификатора сервиса
// и идентификаторов прикладных операций
//
Процедура УдалитьНачальныеИКонечныеПробелыВИдентификаторах()
	
	ИдентификаторСервиса = СокрЛП(ИдентификаторСервиса);
	
	Для Каждого ДанныеОперации Из ПрикладныеОперации Цикл
		ДанныеОперации.ИдентификаторОперации = СокрЛП(ДанныеОперации.ИдентификаторОперации);
	КонецЦикла;
	
КонецПроцедуры

// Проверяет корректность введенных идентификатора сервиса
// и идентификаторов прикладных операций текущего сервиса
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПроверитьИдентификаторыСервисаИОпераций()
	
	ИдентификаторыКорректны = Истина;
	
	// 1. Проверим основной идентификатор сервиса
	РезультатПроверкиИдентификатораСервиса = тм_ТочкаМаршрутизацииСлужебныйФункционал
		.РезультатПроверкиИдентификатораСервисаИлиОперации(ИдентификаторСервиса);
	
	Если НЕ РезультатПроверкиИдентификатораСервиса.ЭтоДопустимыйИдентификатор Тогда
		
		ИдентификаторыКорректны = Ложь;
		
		ТекстОшибки = СтрСоединить(РезультатПроверкиИдентификатораСервиса.ОшибкиЗаполнения, Символы.ПС);
		
		тм_ТочкаМаршрутизацииСлужебныйФункционал.СообщитьОшибкуЗаполненияРеквизитаОбъекта(ТекстОшибки,
			ЭтотОбъект, "ИдентификаторСервиса");
		
	КонецЕсли;
	
	// 2. Проверим идентификаторы прикладных операций
	Для Каждого ДанныеОперации Из ПрикладныеОперации Цикл
		
		РезультатПроверкиИдентификатораОперации = тм_ТочкаМаршрутизацииСлужебныйФункционал
			.РезультатПроверкиИдентификатораСервисаИлиОперации(ДанныеОперации.ИдентификаторОперации);
		
		Если НЕ РезультатПроверкиИдентификатораОперации.ЭтоДопустимыйИдентификатор Тогда
			
			ИдентификаторыКорректны = Ложь;
			
			ТекстОшибки = СтрСоединить(РезультатПроверкиИдентификатораОперации.ОшибкиЗаполнения, Символы.ПС);
			
			тм_ТочкаМаршрутизацииСлужебныйФункционал.СообщитьОшибкуЗаполненияРеквизитаСтрокиТаблицыОбъекта(ТекстОшибки,
				ЭтотОбъект, "ПрикладныеОперации", "ИдентификаторОперации", ДанныеОперации.НомерСтроки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИдентификаторыКорректны;
	
КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли