// MIT License
//
// Copyright (c) 2025 Maksim Kochemasov
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Проверяет наличие прикладной операции с указанным идентификатором
// в переданном внешнем сервисе
//
// Параметры:
//  ВнешнийСервис - СправочникСсылка.тм_ВнешниеСервисы
//  ИдентификаторОперации - Строка
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПрикладнаяОперацияЕстьВСервисе(ВнешнийСервис, ИдентификаторОперации) Экспорт
	
	ПрикладнаяОперацияЕстьВСервисе = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Поле1
	|ИЗ
	|	Справочник.тм_ВнешниеСервисы.ПрикладныеОперации КАК тм_ВнешниеСервисыПрикладныеОперации
	|ГДЕ
	|	тм_ВнешниеСервисыПрикладныеОперации.Ссылка = &ВнешнийСервис
	|	И тм_ВнешниеСервисыПрикладныеОперации.ИдентификаторОперации = &ИдентификаторОперации";
	
	Запрос.УстановитьПараметр("ВнешнийСервис", ВнешнийСервис);
	Запрос.УстановитьПараметр("ИдентификаторОперации", ИдентификаторОперации);
	
	ПрикладнаяОперацияЕстьВСервисе = НЕ Запрос.Выполнить().Пустой();
	
	Возврат ПрикладнаяОперацияЕстьВСервисе;
	
КонецФункции

// Возвращает внешний сервис найденный по указанному идентификатору
//
// Параметры:
//  ИдентификаторСервиса - Строка
// 
// Возвращаемое значение:
//   - СправочникСсылка.тм_ВнешниеСервисы
//
Функция ПолучитьСервисПоИдентификатору(ИдентификаторСервиса) Экспорт
	
	НайденныйСервис = ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тм_ВнешниеСервисы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.тм_ВнешниеСервисы КАК тм_ВнешниеСервисы
	|ГДЕ
	|	тм_ВнешниеСервисы.ИдентификаторСервиса = &ИдентификаторСервиса";
	
	Запрос.УстановитьПараметр("ИдентификаторСервиса", ИдентификаторСервиса);
	
	ВыборкаСервис = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаСервис.Следующий() Тогда
		НайденныйСервис = ВыборкаСервис.Ссылка;
	КонецЕсли;
	
	Возврат НайденныйСервис;
	
КонецФункции

// Возвращает ссылки на модули, необходимые для выполнения прикладной операции сервиса
//
// Параметры:
//  ВнешнийСервис - СправочникСсылка.тм_ВнешниеСервисы - сервиса, содержащий переданную прикладную операцию
//  ИдентификаторОперации - Строка - идентификатор прикладной операции, которую нужно выполнить
// 
// Возвращаемое значение:
//   - Структура:
//       * МодульAPI - ОпределяемыйТип.тм_МодульАпи - ссылка на модуль обработки основного модуля API сервиса
//       * МодульОперации - ОпределяемыйТип.тм_МодульАпи - ссылка на модуль обработки прикладной операции
//
Функция МодулиПрикладнойОперации(ВнешнийСервис, ИдентификаторОперации) Экспорт
	
	МодулиПрикладнойОперации = Новый Структура;
	МодулиПрикладнойОперации.Вставить("МодульAPI");
	МодулиПрикладнойОперации.Вставить("МодульОперации");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тм_ВнешниеСервисы.МодульAPI КАК МодульAPI,
	|	тм_ВнешниеСервисыПрикладныеОперации.МодульОперации КАК МодульОперации
	|ИЗ
	|	Справочник.тм_ВнешниеСервисы КАК тм_ВнешниеСервисы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.тм_ВнешниеСервисы.ПрикладныеОперации КАК тм_ВнешниеСервисыПрикладныеОперации
	|		ПО (тм_ВнешниеСервисы.Ссылка = &ВнешнийСервис)
	|			И тм_ВнешниеСервисы.Ссылка = тм_ВнешниеСервисыПрикладныеОперации.Ссылка
	|			И (тм_ВнешниеСервисыПрикладныеОперации.ИдентификаторОперации = &ИдентификаторОперации)";
	
	Запрос.УстановитьПараметр("ВнешнийСервис", ВнешнийСервис);
	Запрос.УстановитьПараметр("ИдентификаторОперации", ИдентификаторОперации);
	
	ВыборкаДанныеСервиса = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаДанныеСервиса.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(МодулиПрикладнойОперации, ВыборкаДанныеСервиса);
	КонецЕсли;
	
	Возврат МодулиПрикладнойОперации;
	
КонецФункции

// Возвращает ссылку на основной модуль-обработку API переданного сервиса
//
// Параметры:
//  ВнешнийСервис - СправочникСсылка.тм_ВнешниеСервисы
// 
// Возвращаемое значение:
//   - ОпределяемыйТип.тм_МодульАпи - ссылка на обработку основного модуля API
//
Функция ОсновнойМодульСервиса(ВнешнийСервис) Экспорт
	
	ОсновнойМодульAPI = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тм_ВнешниеСервисы.МодульAPI КАК МодульAPI
	|ИЗ
	|	Справочник.тм_ВнешниеСервисы КАК тм_ВнешниеСервисы
	|ГДЕ
	|	тм_ВнешниеСервисы.Ссылка = &ВнешнийСервис";
	
	Запрос.УстановитьПараметр("ВнешнийСервис", ВнешнийСервис);
	
	ВыборкаМодульAPI = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаМодульAPI.Следующий() Тогда
		ОсновнойМодульAPI = ВыборкаМодульAPI.МодульAPI;
	КонецЕсли;
	
	Возврат ОсновнойМодульAPI;
	
КонецФункции

// Проверяет наличие ранее созданного сервиса с переданным идентификатором.
// Для вызова из событий модуля объекта.
//
// Параметры:
//  ИдентификаторСервиса - Строка - проверяемый идентификатор
// 
// Возвращаемое значение:
//   - Булево
//
Функция СервисСИдентификаторомУжеСуществует(ИдентификаторСервиса) Экспорт
	
	Если НЕ ТранзакцияАктивна() Тогда
		ВызватьИсключение НСтр("ru = 'Вызов метода возможен только в транзакции'");
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.тм_ВнешниеСервисы");
	ЭлементБлокировки.УстановитьЗначение("ИдентификаторСервиса", ИдентификаторСервиса);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тм_ВнешниеСервисы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.тм_ВнешниеСервисы КАК тм_ВнешниеСервисы
	|ГДЕ
	|	тм_ВнешниеСервисы.ИдентификаторСервиса = &ИдентификаторСервиса";
	
	Запрос.УстановитьПараметр("ИдентификаторСервиса", ИдентификаторСервиса);
	
	СервисСуществует = НЕ Запрос.Выполнить().Пустой();
	
	Возврат СервисСуществует;
	
КонецФункции

// Возвращает идентфикатор данных аутентификации для переданного сервиса
//
// Параметры:
//  Сервис - СправочникСсылка.тм_ВнешниеСервисы
// 
// Возвращаемое значение:
//   - Строка
//
Функция ИдентификаторАутентификацииСервиса(Сервис) Экспорт
	
	ИдентификаторАутентификацииСервиса = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тм_ВнешниеСервисы.ИдентификаторДанныхАвторизации КАК ИдентификаторДанныхАвторизации
	|ИЗ
	|	Справочник.тм_ВнешниеСервисы КАК тм_ВнешниеСервисы
	|ГДЕ
	|	тм_ВнешниеСервисы.Ссылка = &Сервис";
	
	Запрос.УстановитьПараметр("Сервис", Сервис);
	
	ВыборкаИдентификатор = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаИдентификатор.Следующий() Тогда
		ИдентификаторАутентификацииСервиса = ВыборкаИдентификатор.ИдентификаторДанныхАвторизации;
	КонецЕсли;
	
	Возврат ИдентификаторАутентификацииСервиса;
	
КонецФункции

#КонецОбласти

#КонецЕсли